import heroes.*
import objetos.*
import wollok.game.*
import gestorDeObjetos.*
import enemigos.*

describe "Tests para los heroes" {
	
	const pocionVida = new Pocion(esPocionMana = false, position = game.at(0, 0), image = "potionGreen.png")
	const pocionMana = new Pocion(esPocionMana = true, position = game.at(0, 0), image = "potionBlue.png")
	const armaHacha = new Arma(danioArma = 12, position = game.at(0, 0), image = "hacha.png")
	const armaduraBronce = new Armadura(defensaArmadura = 4, position = game.at(0, 0), image =  "armadurabronce.png")
	const armaduraOro = new Armadura(defensaArmadura = 10, position = game.at(1, 4), image = "armaduraoro.png")
	const armaEspada = new Arma(danioArma = 15, position = game.at(0, 0), image = "espada.png")
	const armaduraPlata = new Armadura(defensaArmadura = 7, position = game.at(0, 0), image = "armaduraplata.png")
	const arbolFuego = new Enemigo(image = "arboldefuego.png", position = game.at(5, 2))
	
	fixture {
		game.clear()
		warrior.position(game.origin())
		gestorDeObjetos.agregar(warrior)
		wizzard.init()
		tank.init()
		warrior.init()
	}
	
	test "heroe equipa y bebe pocion de vida" {
		gestorDeObjetos.agregar(pocionVida)
		assert.equals(200, warrior.actualVida())
		warrior.tomarObjeto()
		assert.equals(#{ pocionVida }, warrior.mochila())
		warrior.actualVida(150)
		assert.equals(150, warrior.actualVida())
		warrior.beberPocionVida()
		assert.equals(#{ }, warrior.mochila())
		assert.equals(200, warrior.actualVida())
	}
	
	test "heroe equipa y bebe pocion de mana" {
		gestorDeObjetos.agregar(pocionMana)
		assert.equals(50, warrior.actualMana())
		warrior.tomarObjeto()
		assert.equals(#{ pocionMana }, warrior.mochila())
		warrior.actualMana(10)
		assert.equals(10, warrior.actualMana())
		warrior.beberPocionMana()
		assert.equals(#{ }, warrior.mochila())
		assert.equals(50, warrior.actualMana())
	}
	
	test "heroe no puede beber pociones porque no tiene" {
		assert.equals(#{ }, warrior.mochila())
		
		assert.throwsException({warrior.beberPocionMana()})
		assert.throwsException({warrior.beberPocionVida()})
	}
	
	test "Heroe toma un arma y se lo equipa" {
		gestorDeObjetos.agregar(armaHacha)
		warrior.tomarObjetoQueTenesDebajo()
		
		assert.equals(armaHacha, warrior.armaEquipada())
	}
	
	test "Heroe se equipa con otra arma diferente al que tiene" {
		warrior.armaEquipada(armaHacha)
		
		gestorDeObjetos.agregar(armaEspada)
		warrior.tomarObjetoQueTenesDebajo()
		
		assert.equals(armaEspada, warrior.armaEquipada())
		assert.equals(armaHacha, warrior.objetoDebajo())
	}
	
	test "Heroe toma una armadura y se lo equipa" {
		gestorDeObjetos.agregar(armaduraBronce)
		warrior.tomarObjetoQueTenesDebajo()
		
		assert.equals(armaduraBronce, warrior.armaduraEquipada())
	}
	
	test "Heroe se equipa con otra armadura diferente al que tiene" {
		warrior.armaduraEquipada(armaduraOro)
		
		gestorDeObjetos.agregar(armaduraPlata)
		warrior.tomarObjetoQueTenesDebajo()
		
		assert.equals(armaduraPlata, warrior.armaduraEquipada())
		assert.equals(armaduraOro, warrior.objetoDebajo())
	}
	
	test "No hay nada que tomar" {
		assert.throwsException({warrior.tomarObjetoQueTenesDebajo()})
	}
	
	test "Heroe (warrior) recibe danio de enemigo sin armadura equipada" {
		arbolFuego.atacar(warrior)
		
		assert.that(warrior.actualVida() < 200)
		//nivel danio: 10 - defensa heroe: (defensa sin armadura: 8 * modificador de defensa: 1,1) => 200 - 1,2 => 198,8
		assert.equals(198.8, warrior.actualVida())
	}
	
	test "Enemigo recibe danio de heroe (warrior) sin arma equipada" {
		warrior.atacar(arbolFuego)
		
		assert.that(arbolFuego.actualVida() < 100)
		// (danio sin arma: 8 * modificador de ataque: 1,5) = 12 => 100 - 12 => 88
		assert.equals(88, arbolFuego.actualVida())
	}
	
	test "Heroe (warrior) recibe danio de enemigo con armadura equipada" {
		warrior.armaduraEquipada(armaduraOro)
		
		arbolFuego.atacar(warrior)
		
		assert.that(warrior.actualVida() < 200)
		//nivel danio: 10 - defensa heroe: (defensa armadura: 10 * modificador de defensa: 1,1) => 200 - 1 => 199
		assert.equals(199, warrior.actualVida())
	}
	
	test "Enemigo recibe danio de heroe (warrior) con arma equipada" {
		warrior.armaEquipada(armaHacha)
		
		warrior.atacar(arbolFuego)
		
		assert.that(arbolFuego.actualVida() < 100)
		// (danio arma: 12 * modificador de ataque: 1,5) = 18 => 100 - 18 => 82
		assert.equals(82, arbolFuego.actualVida())
	}
	
	test "Heroe (wizzard) recibe danio de enemigo sin armadura equipada" {
		arbolFuego.atacar(wizzard)
		
		assert.that(wizzard.actualVida() < 125)
		//nivel danio: 10 - defensa heroe: (defensa sin armadura: 8 * modificador de defensa: 0.8) => 125 - 3.6 => 121.4
		assert.equals(121.4, wizzard.actualVida())
	}
	
	test "Enemigo recibe danio de heroe (wizzard) sin arma equipada" {
		wizzard.atacar(arbolFuego)
		
		assert.that(arbolFuego.actualVida() < 100)
		// (danio sin arma: 8 * modificador de ataque: 0.9) = 12 => 100 - 7.2 => 92.8
		assert.equals(92.8, arbolFuego.actualVida())
	}
	
	test "Heroe (wizzard) recibe danio de enemigo con armadura equipada" {
		wizzard.armaduraEquipada(armaduraOro)
		
		arbolFuego.atacar(wizzard)
		
		assert.that(wizzard.actualVida() < 125)
		//nivel danio: 10 - defensa heroe: (defensa armadura: 10 * modificador de defensa: 0.8) => 125 - 2 => 123
		assert.equals(123, wizzard.actualVida())
	}
	
	test "Enemigo recibe danio de heroe (wizzard) con arma equipada" {
		wizzard.armaEquipada(armaHacha)
		
		wizzard.atacar(arbolFuego)
		
		assert.that(arbolFuego.actualVida() < 100)
		// (danio arma: 12 * modificador de ataque: 0.9) = 18 => 100 - 10.8 => 89.2
		assert.equals(89.2, arbolFuego.actualVida())
	}
	
	test "Heroe (tank) recibe danio de enemigo sin armadura equipada" {
		arbolFuego.atacar(tank)
		
		assert.that(tank.actualVida() < 250)
		//nivel danio: 10 - defensa heroe: (1 (defensa heroe supera nivel danio enemigo) => 250 - 1 => 249
		assert.equals(249, tank.actualVida())
	}
	
	test "Enemigo recibe danio de heroe (tank) sin arma equipada" {
		tank.atacar(arbolFuego)
		
		assert.that(arbolFuego.actualVida() < 100)
		// (danio sin arma: 8 * modificador de ataque: 1.1) = 8.8 => 100 - 8.8 => 91.2
		assert.equals(91.2, arbolFuego.actualVida())
	}
	
	test "Heroe (tank) recibe danio de enemigo con armadura equipada" {
		tank.armaduraEquipada(armaduraOro)
		
		arbolFuego.atacar(tank)
		
		assert.that(tank.actualVida() < 250)
		//nivel danio: 10 - defensa heroe: (1 (defensa heroe supera nivel danio enemigo)) => 250 - 1 => 249
		assert.equals(249, tank.actualVida())
	}
	
	test "Enemigo recibe danio de heroe (tank) con arma equipada" {
		tank.armaEquipada(armaHacha)
		
		tank.atacar(arbolFuego)
		
		assert.that(arbolFuego.actualVida() < 100)
		// (danio arma: 12 * modificador de ataque: 1.1) = 13.2 => 100 - 13.2 => 86.8
		assert.equals(86.8, arbolFuego.actualVida())
	}
	
	test "Heroe (warrior) lanza ataque especial" {
		warrior.lanzarHechizo(arbolFuego)
		assert.that(!warrior.hayMana())
		// danio hecho = 50 * modificador de ataque (1.5) => 75 => 100 -75 => 
		assert.equals(25, arbolFuego.actualVida())
	}
	
	test "Heroe (warrior) no puede lanzar ataque especial" {
		warrior.lanzarHechizo(arbolFuego)
		assert.throwsException({warrior.lanzarHechizo(arbolFuego)})
	}
}